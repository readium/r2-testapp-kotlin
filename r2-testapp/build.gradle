/*
 * Module: r2-testapp-kotlin
 * Developers: Aferdita Muriqi, Cl√©ment Baumann
 *
 * Copyright (c) 2018. European Digital Reading Lab. All rights reserved.
 * Licensed to the Readium Foundation under one or more contributor license agreements.
 * Use of this source code is governed by a BSD-style license which is detailed in the
 * LICENSE file present in the project repository where this source code is maintained.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.readium'


int major = 2
int minor = 1
int patch = 0
int build = 22
String type = ""

def version = "$major.$minor.$patch"
boolean appendBuild = build != 0
if (appendBuild || !type.empty) {
    version += "-$type"
    if (appendBuild) {
        version += build
    }
}

project.ext.versionName = version
project.ext.versionCode = 1_000_000 * major + 10_000 * minor + 100 * patch + build


android {
    compileSdkVersion 29
    //Error: Invoke-customs are only supported starting with Android O (--min-api 26)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId "org.readium.r2reader"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode project.ext.versionCode
        versionName project.ext.versionName
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/androidTest']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
            if (findProject(':r2-lcp')) {
                java.srcDirs = ['src/main/java', 'src/withlcp/java']
            } else {
                java.srcDirs = ['src/main/java', 'src/nondrm/java']
            }
        }

        debug {
            res.srcDirs = ['src/androidTest/res', 'src/main/res']
            assets.srcDirs = ['src/androidTest/assets']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    if (findProject(':r2-shared')) {
        implementation project(':r2-shared')
    } else {
        implementation "com.github.readium:r2-shared-kotlin:1.1.5"
    }
    if (findProject(':r2-navigator')) {
        implementation project(':r2-navigator')
    } else {
        implementation "com.github.readium:r2-navigator-kotlin:1.1.4"
    }
    if (findProject(':r2-streamer')) {
        implementation project(':r2-streamer')
    } else {
        implementation "com.github.readium:r2-streamer-kotlin:1.1.4"
    }
    if (findProject(':r2-opds')) {
        implementation project(':r2-opds')
    } else {
        implementation "com.github.readium:r2-opds-kotlin:1.1.3"
    }
    if (findProject(':r2-lcp')) {
        implementation project(':r2-lcp')
    }


    implementation('com.mcxiaoke.koi:core:0.5.5') {
        exclude module: 'support-v4'
    }
    implementation('com.mcxiaoke.koi:async:0.5.5') {
        exclude module: 'support-v4'
    }

    implementation "androidx.appcompat:appcompat:1.2.0-alpha01"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.webkit:webkit:1.1.0'
    implementation "com.google.android.material:material:1.2.0-alpha03"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation('com.github.edrlab.nanohttpd:nanohttpd-nanolets:2.3.2') {
        exclude group: 'org.parboiled'
    }

    implementation('com.github.edrlab.nanohttpd:nanohttpd:2.3.2') {
        exclude group: 'org.parboiled'
    }

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'org.jetbrains.anko:anko-design:0.10.8'

    // For SnackBars
    implementation 'org.jetbrains.anko:anko-commons:0.10.8'

    // Anko Layouts
    implementation 'org.jetbrains.anko:anko-sdk25:0.10.8'

    // sdk15, sdk19, sdk21, sdk23 are also available
    implementation 'org.jetbrains.anko:anko-appcompat-v7:0.10.8'
    implementation 'org.jetbrains.anko:anko-recyclerview-v7:0.10.8'

    // Coroutine listeners for Anko Layouts
    implementation 'org.jetbrains.anko:anko-sdk25-coroutines:0.10.8'
    implementation 'org.jetbrains.anko:anko-appcompat-v7-coroutines:0.10.8'

    // Anko SQLite
    implementation 'org.jetbrains.anko:anko-sqlite:0.10.8'
    implementation 'com.github.kittinunf.fuel:fuel:1.16.0'

    //for JVM
    implementation 'com.github.kittinunf.fuel:fuel-android:1.16.0'

    // async functions (only ~70k)
    final KOVENANT_VERSION = '3.3.0'
    implementation "nl.komponents.kovenant:kovenant:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-core:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-android:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-combine:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-jvm:$KOVENANT_VERSION"
    implementation "nl.komponents.kovenant:kovenant-functional:$KOVENANT_VERSION"

    implementation 'org.zeroturnaround:zt-zip:1.13'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'org.slf4j:slf4j-nop:1.7.25'

    implementation 'com.duolingo.open:rtl-viewpager:1.0.3'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'com.commonsware.cwac:merge:1.1.2'
    implementation 'com.github.abacritt:sectionedmergeadapter:0.2'
    implementation 'org.jsoup:jsoup:1.10.3'

    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation 'androidx.test:core:1.2.1-alpha02'
    androidTestImplementation 'androidx.test:core-ktx:1.2.1-alpha02'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-alpha02'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2-alpha02'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    testImplementation 'androidx.test:core:1.2.1-alpha02'
    testImplementation 'androidx.test.ext:junit:1.1.2-alpha02'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    testImplementation 'androidx.test.ext:truth:1.3.0-alpha02'
}
